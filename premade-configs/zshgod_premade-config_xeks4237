# Function to declare default configuration for zshgod in case
# user uses prompt without config file generated by configuration wizard
function zshgod_premade-config_xeks4237() {
	# zstyle options to enable or disable some vcs which you don't use
	# zstyle ':vcs_info:*' disable git svn
	zstyle ':vcs_info:*' enable bzr cdv cvs darcs fossil git hg mtn p4 svk svn tla

	# Variable which sets minimal amount of exectime after its shown
	ZSHGOD_EXECTIME_MIN=5

	# Secondry prompt shown when writing functions, loops and etc in prompt manually
	# Expanded in same way as PS1
	PS2='%B%F{#B4BEFE}%S %K{#11111B}%_%k %s%f%b '

	# Prompt used inside of select loop, expanded in same way as PS1
	PS3='%F{#B4BEFE}?#%f '

	# Prompt shown when xtrace, sourcetrace, and etc logging options are set
	PS4='%F{#B4BEFE}%N%f %F{#F9E2AF}%i%f%F{#A6E3A1}:%f '

	# [ Left to right pointing arrow styled functions used in prompt ]
	autoload -Uz \
		zshgod_arrow_left-to-right_git_info \
		zshgod_arrow_left-to-right_time \
		zshgod_arrow_left-to-right_exectime_human-readable \
		zshgod_arrow_left-to-right_current-pwd \
		zshgod_arrow_left-to-right_sshonly_userandhostname \
		zshgod_arrow_left-to-right_root-indicator \
		zshgod_arrow_left-to-right_vcs_branch \
		zshgod_arrow_left-to-right_vcs_system \
		zshgod_arrow_left-to-right_jobs

	# [ Right to left pointing arrow styled functions used in prompt ]
	autoload -Uz \
		zshgod_arrow_right-to-left_sshonly_userandhostname

	# preexec function which gets called before every COMMAND
	preexec() {
		# Function/Command to set title of the terminal
		zshgod_set-title -Pr '%~'
	}

	# precmd function which gets called before showing prompt
	precmd() {
		# Function/Command to set title of the terminal
		zshgod_set-title -Pr '%1~: $2'
	}

	# Array with functions and args for them which are used in main prompt
	# This functions going to be used asynchronously in prompt
	# And they will also create variables with same name as functions,
	# but uppercase and with _ instead of -
	# For Example zsh_info-get => ZSH_INFO_GET
	ZSHGOD_USED_FUNCTIONS=(
		'zshgod_arrow_left-to-right_git_info "#6C7086" "#A6E3A1" "#F9E2AF" "#F38BA8" "#89DCEB" "#B4BEFE"'
		'zshgod_arrow_left-to-right_current-pwd "#89B4FA" "#11111B"'
		'zshgod_arrow_left-to-right_jobs "#CBA6F7" "#11111B"'
		'zshgod_arrow_left-to-right_vcs_system "#B4BEFE" "#11111B"'
		'zshgod_arrow_left-to-right_vcs_branch "#A6E3A1" "#11111B"'
		'zshgod_arrow_left-to-right_exectime_human-readable "$ZSHGOD_EXECTIME" "#F9E2AF" "#11111B"'
		'zshgod_arrow_left-to-right_time "#F9E2AF" "#11111B"'
		'zshgod_arrow_left-to-right_root-indicator "#A6E3A1" "#11111B" "#F38BA8" "#11111B"'
		'zshgod_arrow_right-to-left_sshonly_userandhostname "#A6E3A1" "#11111B" "#F38BA8" "#11111B" "#F9E2AF" "#11111B" "#89B4FA" "#11111B"'
	)

	# Array with items of main prompts left side
	ZSHGOD_LEFT_PROMPT=(
		'%B'
		'┌'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_CURRENT_PWD'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_JOBS'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_VCS_SYSTEM'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_VCS_BRANCH'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_GIT_INFO'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_EXECTIME_HUMAN_READABLE'
		'$prompt_newline'
		'└'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_TIME'
		'$ZSHGOD_ARROW_LEFT_TO_RIGHT_ROOT_INDICATOR'
		' '
		'%b'
	)

	# Array with items of main prompts right side
	ZSHGOD_RIGHT_PROMPT=(
		'%B'
		'$ZSHGOD_ARROW_RIGHT_TO_LEFT_SSHONLY_USERANDHOSTNAME'
		'%b'
	)

	# Set right and left sides of the main prompt using elements of arrays
	# And join them to don't have extra spaces between elements
	PS1="${(j::)ZSHGOD_LEFT_PROMPT}"
	RPS1="${(j::)ZSHGOD_RIGHT_PROMPT}"
}

# Run config function so sourcing this file actually runs it
# And also pass args to function in case function accepts them
zshgod_premade-config_xeks4237 "$@"
