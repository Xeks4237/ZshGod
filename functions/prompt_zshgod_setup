# Function where all other functions are used to make prompt
# This function is called when you choose prompt using 'prompt' command
# This functions name is important it should be 'prompt_<name_of_prompt>_setup'
# And this function also accepts args through 'prompt' command, like:
# prompt zshgod --color-green=#00ff00
prompt_zshgod_setup() {
	# [ Prompt specific opts and Hooks for Functions ]
	# Sets prompt specific zsh opts like prompt substitution
	prompt_opts=( bang cr percent sp subst )

	# Load Zsh module related to zsh hooks
	autoload -Uz add-zsh-hook

	# Builtin zsh module for getting basic info from vcs systems
	autoload -Uz vcs_info

	# preexec hook for recording time when any command was runned, needed for exectime functions
	add-zsh-hook preexec prompt_zshgod_preexec

	# precmd hook for recording time when any command was finished, needed for exectime functions
	add-zsh-hook precmd prompt_zshgod_precmd

	# vcs_info function for gettings info about current vcs
	add-zsh-hook precmd vcs_info

	# prcmd hook for making prompt sparse
	add-zsh-hook precmd prompt_zshgod_sparse-prompt

	# Checks passed flags/args to function to make easy fast configuration
	for arg in "$@"; do
		case "$arg" in
				# [ Flags used to overwrite existing colors ]
			--color-rosewater=*) ZSHGOD_THM_CATPPUCCIN_ROSEWATER="${arg#--color-rosewater=}" ;;

			--color-flamingo=*) ZSHGOD_THM_CATPPUCCIN_FLAMINGO="${arg#--color-flamingo=}" ;;

			--color-pink=*) ZSHGOD_THM_CATPPUCCIN_PINK="${arg#--color-pink=}" ;;

			--color-mauve=*) ZSHGOD_THM_CATPPUCCIN_MAUVE="${arg#--color-mauve=}" ;;

			--color-red=*) ZSHGOD_THM_CATPPUCCIN_RED="${arg#--color-red=}" ;;

			--color-maroon=*) ZSHGOD_THM_CATPPUCCIN_MAROON="${arg#--color-maroon=}" ;;

			--color-peach=*) ZSHGOD_THM_CATPPUCCIN_PEACH="${arg#--color-peach=}" ;;

			--color-yellow=*) ZSHGOD_THM_CATPPUCCIN_YELLOW="${arg#--color-yellow=}" ;;

			--color-green=*) ZSHGOD_THM_CATPPUCCIN_GREEN="${arg#--color-green=}" ;;

			--color-teal=*) ZSHGOD_THM_CATPPUCCIN_TEAL="${arg#--color-teal=}" ;;

			--color-sky=*) ZSHGOD_THM_CATPPUCCIN_SKY="${arg#--color-sky=}" ;;

			--color-sapphire=*) ZSHGOD_THM_CATPPUCCIN_SAPPHIRE="${arg#--color-sapphire=}" ;;

			--color-blue=*) ZSHGOD_THM_CATPPUCCIN_BLUE="${arg#--color-blue=}" ;;

			--color-lavender=*) ZSHGOD_THM_CATPPUCCIN_LAVENDER="${arg#--color-lavender=}" ;;

				# [ Hardlers for weird scenarios ]
			--*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;

			*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;

		esac
	done

	# [ Prompt Scructure ]
	# TODO: Make this variables to fully configurable through args/flags in this function
	# Variable which sets left side of prompt
	PS1='%B$(prompt_zshgod_left-to-right_time)$(prompt_zshgod_left-to-right_root-indicator)%b '

	# Variable which sets right side of prompt
	RPS1='%B$(prompt_zshgod_right-to-left_exectime)$(prompt_zshgod_right-to-left_git_info)$(prompt_zshgod_right-to-left_vcs-info)$(prompt_zshgod_right-to-left_current-pwd)$(prompt_zshgod_right-to-left_sshonly_userandhostname)%b'
}

