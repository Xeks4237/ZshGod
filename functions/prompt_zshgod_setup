# Function where all other functions are used to make prompt
# This function is called when you choose prompt using 'prompt' command
# This functions name is important it should be 'prompt_<name_of_prompt>_setup'
# And this function also accepts args through 'prompt' command, like:
# prompt zshgod --color-green=#00ff00
function prompt_zshgod_setup() {
	# [ Prompt specific opts and Hooks for Functions ]
	# Sets prompt specific zsh opts like prompt substitution
	prompt_opts=( bang cr percent sp subst )

	# Load Zsh module related to zsh hooks
	autoload -Uz add-zsh-hook

	# Builtin zsh module for getting basic info from vcs systems
	autoload -Uz vcs_info

	# Initialize zsh-async library
	autoload -Uz prompt_zshgod_zsh-async-lib
	async_init

	# preexec hook gets called before you run any command, like when you press enter
	# Needed for stuff like exectime functions and etc
	autoload -Uz prompt_zshgod_preexec
	add-zsh-hook preexec prompt_zshgod_preexec

	# precmd hook gets called when running command finishes and before prompt is drown
	# Needed for stuff like exectime functions and etc
	autoload -Uz prompt_zshgod_precmd
	add-zsh-hook precmd prompt_zshgod_precmd

	# [ Configuration Variables ]
	# Variable which sets amount of exectime after exectime is not hided
	ZSHGOD_EXECTIME_MIN=5

	# Disable multilined prompt by default
	ZSHGOD_MULTILINED=false

	# Source default configuration before overwriting it with user defined options
	source "$ZSHGOD_HOME/functions/prompt_zshgod_default-config"

	# Cycle through passed args/flags to function to make easy and fast configuration
	for arg in "$@"; do
		case "$arg" in
				# Flag to open simple configuration wizard/editor
			configure)
				autoload -Uz prompt_zshgod_configure
				prompt_zshgod_configure
				;;

				# Flag to specify config file
			--config-file=*)
				ZSHGOD_CONFIG_FILE="${arg#--config-file=}"
				# Source config file if it exists
				[[ -f $ZSHGOD_CONFIG_FILE ]] && source "$ZSHGOD_CONFIG_FILE"
				;;

				# Flag to set minimal threshold for displaying exectime
			--min-exectime=*)
				ZSHGOD_EXECTIME_MIN="${arg#--min-exectime=}"
				;;

				# Hardlers for weird scenarios
			--*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;

			*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;
		esac
	done

	# Update prompts list
	promptinit
}
