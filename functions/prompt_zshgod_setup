# Function where all other functions are used to make prompt
# This function is called when you choose prompt using 'prompt' command
# This functions name is important it should be 'prompt_<name_of_prompt>_setup'
# And this function also accepts args through 'prompt' command, like:
# prompt zshgod --color-green=#00ff00
function prompt_zshgod_setup() {
	# [ Prompt specific opts and Hooks for Functions ]
	# Sets prompt specific zsh opts like prompt substitution
	prompt_opts=( bang cr percent sp subst )

	# Ensures that variable $prompt_newline is availible
	# This variable needs to be set, usually set by promptinit
	[[ -z $prompt_newline ]] && typeset -g prompt_newline=$'\n%{\r%}'

	# Load zsh modules used in prompt
	zmodload zsh/datetime
	zmodload zsh/zle
	zmodload zsh/parameter
	zmodload zsh/zutil

	# Load Zsh module related to zsh hooks
	autoload -Uz add-zsh-hook

	# Builtin zsh module for getting basic info from vcs systems
	autoload -Uz vcs_info

	# Load zsh-async library if availible, else source source builtin one
	autoload -Uz async || source "$ZSHGOD_HOME/functions/zshgod_lib_zsh-async"

	# Initialize zsh-async library
	async_init

	# Autoload generally needed functions which are used in prompt
	# Autoloading allows to have only used functions in memory
	# INFO: availible functions are mentioned in help message of prompt: prompt -h zshgod
	# WARN: Always use '-Uz' to supress alias expansion and to use zsh styly autoloading
	autoload -Uz \
		prompt_zshgod_precmd \
		prompt_zshgod_preexec \
		prompt_zshgod_chpwd \
		zshgod_set-title \
		zshgod_vcs_branch \
		zshgod_vcs_system \
		zshgod_async-callback \
		zshgod_configure \
		zshgod_default-config

	# preexec hook gets called before you run any command, like when you press enter
	# Needed for stuff like exectime functions and etc
	add-zsh-hook preexec prompt_zshgod_preexec

	# precmd hook gets called when running command finishes and before prompt is drown
	# Needed for stuff like exectime functions and etc
	add-zsh-hook precmd prompt_zshgod_precmd

	# chpwd hook gets called when current working directory changes
	add-zsh-hook chpwd prompt_zshgod_chpwd

	# [ Default valies for variables ]
	# Variable which sets minimal amount of exectime after its shown
	ZSHGOD_EXECTIME_MIN=5

	# Prevent percentage showing up if output doesn't end with a newline
	PROMPT_EOL_MARK=''

	# Run function for setting up default config for prompt
	zshgod_default-config

	# Cycle through passed args/flags to function to make easy and fast configuration
	for arg in "$@"; do
		case "$arg" in
				# Flag to open simple configuration wizard/editor
			configure)
				zshgod_configure
				;;

				# Flag to specify config file
			--config-file=*)
				ZSHGOD_CONFIG_FILE="${arg#--config-file=}"
				# Source config file if it exists
				[[ -f $ZSHGOD_CONFIG_FILE ]] && source "$ZSHGOD_CONFIG_FILE"
				;;

				# Flag to set minimal threshold for displaying exectime
			--min-exectime=*)
				ZSHGOD_EXECTIME_MIN="${arg#--min-exectime=}"
				;;

				# Hardlers for weird scenarios
			--*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;

			*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;
		esac
	done

	# Update prompts list
	promptinit
}
