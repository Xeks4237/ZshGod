# Function where all other functions are used to make prompt
# This function is called when you choose prompt using 'prompt' command
# This functions name is important it should be 'prompt_<name_of_prompt>_setup'
# And this function also accepts args through 'prompt' command, like:
# prompt zshgod --color-green=#00ff00
function prompt_zshgod_setup() {
	# [ Prompt specific opts and Hooks for Functions ]
	# Sets prompt specific zsh opts like prompt substitution
	prompt_opts=( bang cr percent sp subst )

	# Load Zsh module related to zsh hooks
	autoload -Uz add-zsh-hook

	# Builtin zsh module for getting basic info from vcs systems
	autoload -Uz vcs_info

	# [ Autoload used/needed functions ]
	# Autoload functions which are used in prompt, availible functions are mentioned in help page ( prompt -h zshgod )
	# NOTE: Autoloaded functions are practically lazy loaded functions
	# Always use '-Uz' to supress alias expansion and to use zsh styly autoloading
	autoload -Uz \
		prompt_zshgod_precmd \
		prompt_zshgod_preexec \
		prompt_zshgod_chpwd \
		zshgod_configure \
		zshgod_vcs_branch \
		zshgod_vcs_system \
		zshgod_lib_zsh-async \
		zshgod_async-callback \
		zshgod_default-config

	# [ Autoload rectangle/square styled functions ]
	autoload -Uz \
		zshgod_rectangular_git_dirty \
		zshgod_rectangular_git_info \
		zshgod_rectangular_time \
		zshgod_rectangular_exectime_seconds-only \
		zshgod_rectangular_exectime_human-readable \
		zshgod_rectangular_current-pwd \
		zshgod_rectangular_userandhostname \
		zshgod_rectangular_sshonly_userandhostname \
		zshgod_rectangular_root-indicator \
		zshgod_rectangular_username \
		zshgod_rectangular_sshonly_username \
		zshgod_rectangular_hostname \
		zshgod_rectangular_sshonly_hostname \
		zshgod_rectangular_vcs_branch \
		zshgod_rectangular_vcs_system \
		zshgod_rectangular_jobs

	# [ Autoload left to right pointing arrow styled functions ]
	autoload -Uz \
		zshgod_arrow_left-to-right_git_dirty \
		zshgod_arrow_left-to-right_git_info \
		zshgod_arrow_left-to-right_time \
		zshgod_arrow_left-to-right_exectime_seconds-only \
		zshgod_arrow_left-to-right_exectime_human-readable \
		zshgod_arrow_left-to-right_current-pwd \
		zshgod_arrow_left-to-right_userandhostname \
		zshgod_arrow_left-to-right_sshonly_userandhostname \
		zshgod_arrow_left-to-right_root-indicator \
		zshgod_arrow_left-to-right_username \
		zshgod_arrow_left-to-right_sshonly_username \
		zshgod_arrow_left-to-right_hostname \
		zshgod_arrow_left-to-right_sshonly_hostname \
		zshgod_arrow_left-to-right_vcs_branch \
		zshgod_arrow_left-to-right_vcs_system \
		zshgod_arrow_left-to-right_jobs

	# [ Autoload right to left pointing arrow styled functions ]
	autoload -Uz \
		zshgod_arrow_right-to-left_git_dirty \
		zshgod_arrow_right-to-left_git_info \
		zshgod_arrow_right-to-left_time \
		zshgod_arrow_right-to-left_exectime_seconds-only \
		zshgod_arrow_right-to-left_exectime_human-readable \
		zshgod_arrow_right-to-left_current-pwd \
		zshgod_arrow_right-to-left_userandhostname \
		zshgod_arrow_right-to-left_sshonly_userandhostname \
		zshgod_arrow_right-to-left_root-indicator \
		zshgod_arrow_right-to-left_username \
		zshgod_arrow_right-to-left_sshonly_username \
		zshgod_arrow_right-to-left_hostname \
		zshgod_arrow_right-to-left_sshonly_hostname \
		zshgod_arrow_right-to-left_vcs_branch \
		zshgod_arrow_right-to-left_vcs_system \
		zshgod_arrow_right-to-left_jobs

	# Initialize zsh-async library
	async_init

	# preexec hook gets called before you run any command, like when you press enter
	# Needed for stuff like exectime functions and etc
	add-zsh-hook preexec prompt_zshgod_preexec

	# precmd hook gets called when running command finishes and before prompt is drown
	# Needed for stuff like exectime functions and etc
	add-zsh-hook precmd prompt_zshgod_precmd

	# chpwd hook gets called when current working directory changes
	add-zsh-hook chpwd prompt_zshgod_chpwd

	# [ Configuration Variables ]
	# Variable which sets amount of exectime after exectime is not hided
	ZSHGOD_EXECTIME_MIN=5

	# Disable multilined prompt by default
	ZSHGOD_MULTILINED=false

	# Run function for setting up default config for prompt
	zshgod_default-config

	# Cycle through passed args/flags to function to make easy and fast configuration
	for arg in "$@"; do
		case "$arg" in
				# Flag to open simple configuration wizard/editor
			configure)
				zshgod_configure
				;;

				# Flag to specify config file
			--config-file=*)
				ZSHGOD_CONFIG_FILE="${arg#--config-file=}"
				# Source config file if it exists
				[[ -f $ZSHGOD_CONFIG_FILE ]] && source "$ZSHGOD_CONFIG_FILE"
				;;

				# Flag to set minimal threshold for displaying exectime
			--min-exectime=*)
				ZSHGOD_EXECTIME_MIN="${arg#--min-exectime=}"
				;;

				# Hardlers for weird scenarios
			--*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;

			*)
				print -u2 "Unknown option(s): $arg" >&2
				return 1
				;;
		esac
	done

	# Update prompts list
	promptinit

	# Set right and left sides of the prompt using elements of arrays
	# And join them to don't have extra spaces between elements
	PS1="${(j::)ZSHGOD_LEFT_PROMPT}"
	RPS1="${(j::)ZSHGOD_RIGHT_PROMPT}"
}
