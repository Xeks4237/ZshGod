# Function for checking git repos if they are dirty
function prompt_zshgod_git_info() {
	# Silently exit if not inside a git repository
	command git rev-parse --is-inside-work-tree &>/dev/null || return

	# Fetch machine-readable git status with branch info (includes ahead/behind)
	local git_info="$(command git status --porcelain --branch 2>/dev/null)"

	# If nothing to report (repo clean), output nothing
	[[ -z "$git_info" ]] && return

	# Extract the branch line (starts with "##")
	local branch_line=$(echo "$git_info" | grep "^##")

	# Count how many commits are ahead of upstream (not pushed)
	local ahead_count=0
	if [[ $branch_line =~ "ahead ([0-9]+)" ]]; then
		ahead_count=${match[1]}
	fi

	# Remove branch line to leave only file status lines
	local status_lines=$(echo "$git_info" | grep -v "^##")

	# Count staged changes (new files added, modifications staged, deletes staged, etc.)
	local staged_count=$(echo "$status_lines" | grep -c "^[MADRC] ")

	# Count unstaged modifications to tracked files
	local modified_count=$(echo "$status_lines" | grep -c "^ [MAD] ")

	# Count deleted files (staged + unstaged deletes)
	local deleted_count=$(( $(echo "$status_lines" | grep -c "^[D] ") + $(echo "$status_lines" | grep -c "^ [D] ") ))

	# Count untracked/new files
	local untracked_count=$(echo "$status_lines" | grep -c "^?? ")

	# Build the output parts only if the count is greater than 0
	local parts=()

	(( staged_count > 0 )) && parts+=("%F{$ZSHGOD_THEME_CATPPUCCIN_GREEN}${staged_count}+%f")
	(( modified_count > 0 )) && parts+=("%F{$ZSHGOD_THEME_CATPPUCCIN_YELLOW}${modified_count}*%f")
	(( deleted_count > 0 )) && parts+=("%F{$ZSHGOD_THEME_CATPPUCCIN_RED}${deleted_count}-%f")
	(( untracked_count > 0 )) && parts+=("%F{$ZSHGOD_THEME_CATPPUCCIN_SKY}${untracked_count}?%f")
	(( ahead_count > 0 )) && parts+=("%F{$ZSHGOD_THEME_CATPPUCCIN_LAVENDER}${ahead_count}â†‘%f")

	# If there's anything to show, join the parts add do some customization
	if (( ${#parts[@]} > 0 )); then
		print "%F{$ZSHGOD_THEME_CATPPUCCIN_YELLOW}%K{$ZSHGOD_THEME_CATPPUCCIN_OVERLAY_0} ${parts[*]} %k%f"
	fi
}
